#compdef git-tag git-archive
local curcontext="$curcontext" state state_descr line
typeset -A opt_args
local opts
opts=(
'(1 -)'{'-h','-H'}'[help]'
'(-a)-s[Sign]'
'(-s)-a[Do not sign]'
'-f[force even if tag does not start with v]'
'(-T)-t[Set GPG_TTY to current tty]'
'(-t)-T[Do not modify GPG_TTY]'
)
case $service in
(*tag*)
	opts+=(
'-d[Delete tag locally]'
'(-d -r)-D[Delete tag locally and remotely]'
'-r[Remove tag remotely]'
);;
(*)
	opts+=(
'(-P)-p[TZ=\::/usr/share/zoneinfo/US/Pacific-New]'
'(-p)-P+[Set TZ to specficied value for zip]:timezone:_time_zone'
)
esac
_arguments -C -s -S -A '-*' : $opts \
'1::tag:->tag'
local ret=$?
case $state in
(tag)
	local tags
	tags=(${(f)"$(_call_program tagrefs git tag -l 2>/dev/null)"})
	(( $? )) && {
		_message 'not a git repository'
		return 1
	}
	_wanted tags expl tag compadd -M 'r:|/=**' "$@" -a - tags
	ret=$?
esac
return ret
