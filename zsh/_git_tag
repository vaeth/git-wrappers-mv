#compdef git-tag git-archive
local curcontext="$curcontext" state state_descr line
typeset -A opt_args
local opts
opts=(
'(1 -)'{'-h','-H','-\\?'}'[help]'
'(-a)-s[Sign tag]'
'(-s)-a[Do not sign tag]'
'-f[force even if tag does not start with v]'
'(-T)-t[Set GPG_TTY to current tty]'
'(-t)-T[Do not modify GPG_TTY]'
)
case $service in
(*tag*)
	opts+=(
'-d[Delete tag locally]'
'(-d -r)-D[Delete tag locally and remotely]'
'-r[Remove tag remotely]'
);;
esac
_arguments -C -s -S -A '-*' : $opts \
'1::tag:->tag'
local ret=$?
case $state in
(tag)
	local tags
	tags=(${${(f)"$(_call_program tagrefs git for-each-ref --format='"%(refname)"' refs/tags 2>/dev/null)"}#refs/tags/})
	if (( ${#pipestatus:#0} > 0 )); then
		_message 'not a git repository'
		return 1
	fi
	_wanted tags expl tag compadd -M 'r:|/=**' "$@" -a - tags
	ret=$?
esac
return ret
