#compdef git-commit
local i
i=2
while ((i <= CURRENT))
do	[[ $words[i] = -* ]] && {
		((++i))
		continue
	}
	((++i))
	break
done
if ((i <= CURRENT))
then	shift $((i-1)) words
	words=(git commit $words)
	((CURRENT-=i-3))
	_normal
	return
fi
local curcontext="$curcontext" state state_descr line
typeset -A opt_args
_arguments -C -s -S -A '-*' : \
'(1 -)'{'-h','-H'}'[help]' \
'-t[Set GPG_TTY to current tty]' \
'-T[Do not modify GPG_TTY]' \
'1:mode:->mode'
local ret=$?
case $state in
(mode)
	local mode
	mode=(
	'signed:make a signed commit'
	'unsigned:make an unsigned commit'
	'help:show help'
	)
	_describe -t mode 'mode' mode && ret=0;;
esac
return ret
